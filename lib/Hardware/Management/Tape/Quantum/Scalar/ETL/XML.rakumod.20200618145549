need    Hypervisor::IBM::POWER::HMC::REST::Atom;
use     Terminal::ANSIColor;
use     URI;
use     XML;
unit    role Hypervisor::IBM::POWER::HMC::REST::ETL::XML:api<1>:auth<Mark Devine (mark@markdevine.com)>;

proto method etl-branches (:$TAG is required, :$xml is required, Bool :$optional --> Array) { * };
multi method etl-branches (Str:D :$TAG is required, XML::Document:D :$xml is required, Bool :$optional --> Array) {
    self.config.diag.post: sprintf("%-20s%11s:%12s", &?ROUTINE.name, 'TRANSFORM', 'XML::Document -> XML::Element') if %*ENV<HIPH_ETL_BRANCHES>;
    my XML::Element $root = $xml.root;
    self.etl-branches(:$TAG, :xml($root), :$optional);
}
multi method etl-branches (Str:D :$TAG is required, XML::Element:D :$xml is required, Bool :$optional --> Array) {
    my @xml-nodes = $xml.elements(:$TAG);
    self.config.diag.post: sprintf("%-20s%11s:%12d elements of <%s>", &?ROUTINE.name, 'GATHER', @xml-nodes.elems, $TAG) if %*ENV<HIPH_ETL_BRANCHES>;
    return @xml-nodes if @xml-nodes.elems;
    return Array.new() if $optional;
    self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': Expected TAG <' ~ $TAG ~ '> not found in xml source';
}
multi method etl-branches (Str:D :$TAG is required, XML::Element :$xml is required, Bool :$optional --> Array) {
    return Array.new() if $optional;
    self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': Expected TAG <' ~ $TAG ~ '> not found in empty xml source';
}

proto method etl-branch (:$TAG is required, :$xml is required, Bool :$optional --> XML::Element) { * };
multi method etl-branch (Str:D :$TAG is required, XML::Document:D :$xml is required, Bool :$optional --> XML::Element) {
    self.config.diag.post: sprintf("%-20s%11s:%13s%s", &?ROUTINE.name, 'TRANSFORM', ' ', 'XML::Document -> XML::Element') if %*ENV<HIPH_ETL_BRANCH>;
    my XML::Element $root = $xml.root;
    self.etl-branch(:$TAG, :xml($root), :$optional);
};
multi method etl-branch (Str:D :$TAG is required, XML::Element:D :$xml is required, Bool :$optional --> XML::Element) {
    my $xml-node = $xml.elements(:$TAG, :SINGLE);
    self.config.diag.post: sprintf("%-20s%11s:%13s%s", &?ROUTINE.name, 'ISOLATE', ' ', $TAG) if %*ENV<HIPH_ETL_BRANCH>;
    return $xml-node if $xml-node;
    return XML::Element if $optional;
    self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': Expected TAG <' ~ $TAG ~ '> not found in xml source';
};
multi method etl-branch (Str :$TAG is required, :$xml is required, Bool :$optional --> XML::Element) {
    return XML::Element if $optional;
    self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': TAG was expected';
}

proto method etl-text (:$TAG is required, :$xml is required, Bool :$optional --> Str) { * };
multi method etl-text (Str:D :$TAG is required, XML::Document:D :$xml is required, Bool :$optional --> Str) {
    self.config.diag.post: sprintf("%-20s%11s:%13s%s", &?ROUTINE.name, 'TRANSFORM', ' ', 'XML::Document -> XML::Element') if %*ENV<HIPH_ETL_TEXT_TRANSFORM>;
    my XML::Element $root = $xml.root;
    self.etl-text(:$TAG, :xml($root), :$optional);
};
multi method etl-text (Str:D :$TAG is required, XML::Element:D :$xml is required, Bool :$optional --> Str) {
    my $xml-node = $xml.elements(:$TAG, :SINGLE);
    my $routine-name = &?ROUTINE.name;
    if $xml-node.can('nodes') && $xml-node.nodes.elems && $xml-node.nodes[0].text {
        self.config.diag.post: sprintf("%-20s%11s:%13s<%s> = '%s'", $routine-name, 'EXTRACT', ' ', $TAG, $xml-node.nodes[0].text.trim) if %*ENV<HIPH_ETL_TEXT_EXTRACT>;
        return $xml-node.nodes[0].text.trim;
    }
    self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': Expected TAG <' ~ $TAG ~ '> not found in xml source' unless $optional;
    Nil
};
multi method etl-text (Str:D :$TAG is required, XML::Element :$xml is required, Bool :$optional --> Str) {
    self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': Expected TAG <' ~ $TAG ~ '> not found in empty xml source' unless $optional;
    Nil
};

proto method etl-texts (:$TAG is required, :$xml is required, Bool :$optional --> Str) { * };
multi method etl-texts (Str:D :$TAG is required, XML::Document:D :$xml is required, Bool :$optional --> Str) {
    self.config.diag.post: sprintf("%-20s%11s:%13s%s", &?ROUTINE.name, 'TRANSFORM', ' ', 'XML::Document -> XML::Element') if %*ENV<HIPH_ETL_TEXTS>;
    note colored(sprintf("%-20s %10s: %11s %s", &?ROUTINE.name, 'TRANSFORM', 't' ~ $*THREAD.id, 'XML::Document -> XML::Element'), 'black on_magenta') if %*ENV<HIPH_ETL_TEXTS>;
    my XML::Element $root = $xml.root;
    self.etl-texts(:$TAG, :xml($root), :$optional);
}
multi method etl-texts (Str:D :$TAG is required, XML::Element :$xml is required, Bool :$optional --> Array) {
    my @xml-nodes = $xml.elements(:$TAG);
    unless @xml-nodes.elems {
        return Array.new() if $optional;
        self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': Expected TAG <' ~ $TAG ~ '> not found in xml source';
    }
    my @texts;
    for @xml-nodes -> $xml-node {
        @texts.push: $xml-node.nodes[0].text.trim;
    }
    self.config.diag.post: sprintf("%-20s%11s:%12d of <%s>", &?ROUTINE.name, 'EXTRACT', @texts.elems, $TAG) if %*ENV<HIPH_ETL_TEXTS>;
    return @texts if @texts.elems;
    Nil;
}

proto method etl-links-URIs (:$xml is required --> Array) { * };
multi method etl-links-URIs (XML::Element:D :$xml is required --> Array) {
    my @hrefs;
    for $xml.elements(:TAG<link>) -> $link {
        push @hrefs, URI.new($link.attribs<href>);
    }
    self.config.diag.post: sprintf("%-20s%11s:%12d URIs", &?ROUTINE.name, 'EXTRACT', @hrefs.elems) if %*ENV<HIPH_ETL_LINKS_URIS>;
    return @hrefs;
}
multi method etl-links-URIs (XML::Element :$xml is required --> Array) {
    return Array.new();
}

proto method etl-href (:$xml is required) { * };
multi method etl-href (XML::Element:D :$xml is required --> URI) {
    my $url = $xml.attribs<href>;
    self.config.diag.post: sprintf("%-20s%11s:%13s<%s>", &?ROUTINE.name, 'EXTRACT', ' ', $url) if %*ENV<HIPH_ETL_HREF>;
    return URI.new($url);
}
multi method etl-href (XML::Element :$xml is required) {
    return Nil;
}

method xml-name-exceptions () { ... };

method etl-node-name-check () {
    return unless self.xml.DEFINITE;
    for self.xml.elements -> $element {
        next if self.can($element.name) || $element.name (elem) self.xml-name-exceptions;
        self.config.note.post: self.^name ~ '::' ~ &?ROUTINE.name ~ ': ' ~ $element.name ~ ' not implemented';
    }
}

method etl-atom (:$xml is required) {
    my XML::Element $xml-Atom       = self.etl-branch(:TAG<Atom>,       :$xml);
    my              $AtomID         = self.etl-text(:TAG<AtomID>,       :xml($xml-Atom));
    my              $AtomCreated    = self.etl-text(:TAG<AtomCreated>,  :xml($xml-Atom));
    $AtomCreated                    = DateTime.new($AtomCreated / 1000);
    return Hypervisor::IBM::POWER::HMC::REST::Atom.new(:$AtomID, :$AtomCreated);
}

=finish
